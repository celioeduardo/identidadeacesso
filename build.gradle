buildscript {
    ext {
        springBootVersion = '1.2.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
  id 'net.researchgate.release' version '2.0.2'
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot' 
apply plugin: 'maven' 

sourceCompatibility = 1.8

jar {
    baseName = 'idhadrion'
}

description = "Identidade e Acesso"

repositories { 
	maven { url "http://repo.hadrion.com.br:8081/nexus/content/groups/public" }
	mavenLocal()
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.hateoas:spring-hateoas")
    compile("org.springframework.plugin:spring-plugin-core:1.1.0.RELEASE")
    compile("com.oracle:ojdbc6:11.2.0")
    
    compile("commons-lang:commons-lang:2.3")
    testCompile("com.h2database:h2")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("com.jayway.jsonpath:json-path:1.2.0")
}

ext.priv = parseConfig(file('private.properties'))

def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}

bootRepackage {
	enabled = false
}

createReleaseTag.dependsOn uploadArchives

install {
    repositories.mavenInstaller {
        pom.project {
          parent { 
            groupId 'org.springframework.boot'
            artifactId 'spring-boot-starter-parent'
            version "${springBootVersion}"
          }
        }
    }
}

uploadArchives {
	repositories {
	   mavenDeployer {
			repository(url: priv.host+'/releases') {
				authentication(
					userName: priv.user, password: priv.pass
				)
			}
			snapshotRepository(url: priv.host) {
                authentication(
					userName: priv.user, password: priv.pass
				)
            }
			pom.project {
	          parent { 
	            groupId 'org.springframework.boot'
	            artifactId 'spring-boot-starter-parent'
	            version "${springBootVersion}"
	          }
	        }
	   }
	}
}

